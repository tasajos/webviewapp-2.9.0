// Main build script for WebView App
// 
// Usage: gradlew assembleDebug           Build debug APK
//        gradlew assembleRelease         Build release APK

final VERSION_MAJOR = 3 // max two digits
final VERSION_MINOR = 2 // max two digits
final VERSION_PATCH = 3 // max two digits
final VERSION_BUILD = 2 // max three digits

apply plugin: "com.onesignal.androidsdk.onesignal-gradle-plugin"
apply plugin: "com.android.application"
apply from: "../utils.gradle"

android {
	compileSdkVersion 32
	buildToolsVersion "32.0.0"

	defaultConfig {
		applicationId "chakuy.tsf_2022"
		minSdkVersion 21
		targetSdkVersion 32
		versionCode getVersionCode(VERSION_MAJOR, VERSION_MINOR, VERSION_PATCH, VERSION_BUILD)
		versionName getVersionName(VERSION_MAJOR, VERSION_MINOR, VERSION_PATCH)
		vectorDrawables.useSupportLibrary = true
	}

	signingConfigs {
		release {
			def keystoreProperties = getKeystoreProperties file("../${project.property('keystore.properties')}")
			storeFile file("../${project.property('keystore.file')}")
			storePassword keystoreProperties["keystore.store.password"]
			keyAlias keystoreProperties["keystore.key.alias"]
			keyPassword keystoreProperties["keystore.key.password"]
		}
	}

	buildTypes {
		debug {
			buildConfigField "boolean", "LOGS", "true"
			buildConfigField "boolean", "DEV_ENVIRONMENT", "true"
			buildConfigField "boolean", "TEST_ADS", "false"

			versionNameSuffix "-debug"
		}

		release {
			buildConfigField "boolean", "LOGS", "false"
			buildConfigField "boolean", "DEV_ENVIRONMENT", "false"
			buildConfigField "boolean", "TEST_ADS", "false"

			signingConfig signingConfigs.release
			zipAlignEnabled true
			minifyEnabled false
			shrinkResources false
			proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
		}
	}

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_11
		targetCompatibility JavaVersion.VERSION_11
	}

	setProperty("archivesBaseName", getArtifactName(defaultConfig))
}

dependencies {
	implementation files("libs/alfonz-utility-0.9.4.aar")
	implementation files("libs/alfonz-view-0.9.4.aar")
	implementation files("libs/config.aar")
	implementation "androidx.appcompat:appcompat:1.4.1"
	implementation "androidx.fragment:fragment:1.4.1"
	implementation "com.google.android.ads.consent:consent-library:1.0.8"
	implementation "com.google.android.material:material:1.5.0"
	implementation "com.google.android.play:core:1.10.3"
	implementation "com.google.firebase:firebase-ads:20.6.0"
	implementation "com.google.firebase:firebase-analytics:20.1.0"
	implementation "com.google.firebase:firebase-messaging:23.0.0"
	implementation "com.onesignal:OneSignal:4.6.7"
}

apply plugin: "com.google.gms.google-services"
